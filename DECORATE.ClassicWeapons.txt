ACTOR ClassicFist : Weapon Replaces Fist
{
	Game Doom
	Weapon.Kickback 100
	Obituary "$OB_MPFIST"
	Tag "$TAG_FIST"
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOAUTOAIM
	States
	{
	Ready:
	    PKFS A 1 A_WeaponReady
		Loop
	Deselect:
		PKFS A 1 A_Lower
		Loop
	Select:
		PKFS A 1 A_Raise
		Loop
	Fire:
	    TNT1 A 0 A_jumpifinventory("PowerStrength",1,"Fire2")
		PKFS B 1 A_Giveinventory("Punching",1)
		TNT1 A 0 A_PlaySound("skeleton/swing")
		PKFS CD 1
		PKFS E 2 A_FireCustomMissile("MeleeStrike2", 0, 0, 0, -5)
		PKFS FGHIJKL 2
		Goto Ready
	Fire2:
		PKFS B 1 A_Giveinventory("Punching",1)
		TNT1 A 0 A_PlaySound("weapons/gswing")
		PKFS CD 1
		PKFS E 2 A_FireCustomMissile("MeleeStrike2Smash", 0, 0, 0, -5)
		PKFS FGHIJK 2
		Goto Ready	
	}
}

ACTOR KickAttack: FastProjectile
{
	Radius 18
	Height 6
	DamageType Kick
	Projectile 
	+FORCEXYBILLBOARD
	+NOEXTREMEDEATH
	RenderStyle Add
	Alpha 0.6
    Damage (random(28,32))
    Speed 30
	//HitObituary "$OB_IMPHIT"
	SeeSound "none"
	DeathSound "none"
	Decal "none"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
        TNT1 A 1 //A_PlaySound("weapons/gswing")
		Stop
	Death:
	XDEath:
			TNT1 A 0
			TNT1 A 0 A_CheckFloor("DeathOnGround")
		    PUFF A 0 A_PlaySound("player/cyborg/fist", 3)
			TNT1 A 0 A_SpawnItemEx ("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
			EXPL AAAAAA 0 A_CustomMissile ("MeleeSmoke", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 A 0 Radius_Quake (2, 6, 0, 5, 0)//(intensity, duration, damrad, tremrad, tid)
			
			TNT1 A 10
		Stop
	
	DeathOnGround:
			TNT1 A 0
		    PUFF A 0 A_PlaySound("player/cyborg/fist", 3)
			TNT1 A 0 A_SpawnItemEx ("PLOFT2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
			EXPL AAAAAA 0 A_CustomMissile ("MeleeSmoke", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 A 0 Radius_Quake (2, 6, 0, 5, 0)//(intensity, duration, damrad, tremrad, tid)
			TNT1 A 0 A_Explode(10, 40, 0)
			TNT1 A 10
		Stop
	}
}

ACTOR MeleeStrike1: KickAttack
{
DamageType Melee
Damage (random(10,15))
Speed 30
States
{
	Spawn:
		TNT1 A 1 BRIGHT
		Stop
	Death:
	    TNT1 A 0
		PUFF A 0 A_PlaySound("player/cyborg/fist")
		EXPL AAAAAA 0 A_CustomMissile ("MeleeSmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 10
		Stop
}
}

ACTOR MeleeStrike2: MeleeStrike1
{
Damage (random(24,32))
Speed 40
}

ACTOR MeleeStrike2Smash: MeleeStrike1
{
Damage (random(58,64))
Speed 40
DamageType Extremepunches
}

ACTOR ClassicSaw : Weapon Replaces ChainSaw
{
	Game Doom
	Weapon.Kickback 0
	Weapon.UpSound "weapons/sawup"
	Weapon.ReadySound "none"
	Inventory.PickupMessage "$GOTCHAINSAW"
	Obituary "$OB_MPCHAINSAW"
	Tag "$TAG_CHAINSAW"
	+WEAPON.MELEEWEAPON
	+WEAPON.AXEBLOOD
	States
	{
	Ready:
		TNT1 A 0
		TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
		TNT1 A 0 A_PlaySound("weapons/sawidle", 2)
		TNT1 A 0 A_JumpIfInventory("PowerBloodOnVisor",1,"Ready.Blood")
        TNT1 A 0 A_JumpIfInventory("PowerBlueBloodOnVisor",1,"Ready.BlueBlood")
        TNT1 A 0 A_JumpIfInventory("PowerGreenBloodOnVisor",1,"Ready.GreenBlood")
		
		VSAW ABAB 2 A_WeaponReady
		Loop
		
	Ready.Blood:
		VSRW ABAB 2 A_WeaponReady
		Goto Ready
		
	Ready.BlueBlood:
		VSBW ABAB 2 A_WeaponReady
		Goto Ready
		
	Ready.GreenBlood:
		VSGW ABAB 2 A_WeaponReady
		Goto Ready	
		
	Deselect:
		VSAW A 1 A_Lower
		Loop
	Select:
		VSAW A 1 A_Raise
		
		Loop
	Fire:
		TNT1 A 0
		TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
		TNT1 A 0 A_JumpIfInventory("PowerBloodOnVisor",1,"Fire.Blood")
        TNT1 A 0 A_JumpIfInventory("PowerBlueBloodOnVisor",1,"Fire.BlueBlood")
        TNT1 A 0 A_JumpIfInventory("PowerGreenBloodOnVisor",1,"Fire.GreenBlood")
		VSAW CD 2 A_Saw("weapons/chainsaw/loop", "CSAWCUT", 3, "SSawPuff", SF_NOPULLIN | SF_NOTURN)
		SAWG B 0 A_ReFire
		Goto Ready
		
	Fire.Blood:
		VSRW CD 2 A_Saw("weapons/chainsaw/loop", "CSAWCUT", 3, "SSawPuff", SF_NOPULLIN | SF_NOTURN)
		SAWG B 0 A_ReFire
		Goto Ready
	
	Fire.BlueBlood:
		VSBW CD 2 A_Saw("weapons/chainsaw/loop", "CSAWCUT", 3, "SSawPuff", SF_NOPULLIN | SF_NOTURN)
		SAWG B 0 A_ReFire
		Goto Ready
		
	Fire.GreenBlood:
		VSGW CD 2 A_Saw("weapons/chainsaw/loop", "CSAWCUT", 3, "SSawPuff", SF_NOPULLIN | SF_NOTURN)
		SAWG B 0 A_ReFire
		Goto Ready
	
	Spawn:
		CSAW A -1
		Stop
	}
}

ACTOR ClassicPistol : DoomWeapon Replaces Pistol
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Obituary "$OB_MPPISTOL"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "$TAG_PISTOL"
	States
	{
	Ready:
	    PKPI A 1 A_WeaponReady
		Loop
	Deselect:
		PKPI A 1 A_Lower
		Loop
	Select:
		PKPI A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_FireBullets(5.6, 0, 1, 5, "HitPuff")
		TNT1 A 0 A_PlaySound("weapons/rifle")
		RIFF A 0 A_FireCustomMissile("Tracer", random(-1,1), 0, -1, 0)
		TNT1 A 0 A_SetPitch(-1.0 + pitch)
		PKPI F 1 BRIGHT
		PKPI CDE 1 A_SetPitch(0.25 + pitch)
		TNT1 A 0 A_FireCustomMissile("RifleCaseSpawn",5,0,8,-9)
		PKPI D 1
		PKPI C 1 A_WeaponReady(1)
		PKPI B 0 A_WeaponReady(1)
		PKPI A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
 	Spawn:
		PIST A -1
		Stop
	}
}




ACTOR ClassicShotgun : DoomWeapon Replaces Shotgun
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	+WEAPON.NOAUTOAIM
	Inventory.PickupMessage "$GOTSHOTGUN"
	Obituary "$OB_MPSHOTGUN"
	Tag "$TAG_SHOTGUN"
	States
	{
	Ready:
	    PKSG A 1 A_WeaponReady
		Loop
	Deselect:
	    PKSG A 1 A_Lower
		Loop
	Select:
		PKSG A 1 A_Raise
		Loop
	Fire:
		PKSG A 1
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer", random(-5,5),0,-1,-12,0,random(-1,1))
		TNT1 A 0 A_FireBullets(5.6, 0, 7, 5, "ShotgunPuff")
		TNT1 A 0 A_PlaySound("weapons/sg", 1)
		PKSG B 2 BRIGHT A_SetPitch(-4.0 + pitch)
		PKSG C 1
		PKSG CDDE 1 A_SetPitch(1.0 + pitch)
		PKSG A 4
		PKSG FGHI 1
		TNT1 A 0 A_FireCustomMissile ("ShotCaseSpawn", 0, 0, -4, -4)
		SHTG A 0 A_PlaySound("weapons/sgpump", 3)
		PKSG JKKLLMMMLLKKJIHGF 1
		PKSG A 3
		TNT1 A 0 A_Refire
		PKSG A 10 A_WeaponReady
		Goto Ready
	Spawn:
		SHOT A -1
		Stop
	}
}	




ACTOR ClassicSSG : DoomWeapon Replaces SuperShotgun
{
	Game Doom
	Weapon.AmmoUse 2
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Inventory.PickupMessage "$GOTSHOTGUN2"
	Obituary "$OB_MPSSHOTGUN"
	Tag "$TAG_SUPERSHOTGUN"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	States
	{
	Ready:
	    PKSS A 1 A_WeaponReady
		Loop
	Deselect:
		PKSS A 1 A_Lower
		Loop
	Select:
		PKSS A 1 A_Raise
		Loop
	Fire:
	    PKSS A 1
		TNT1 A 0 A_firebullets (0,0,1,15,"shotpuff",0,180)
		TNT1 AAAAAAAAAAAAAA 0 A_FireCustomMissile("Tracer", random(-10,10),0,-1,-12,0,random(-3,3))
		TNT1 A 0 A_FireBullets(11.2, 7.1, 19, 5, "ShotgunPuff")
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle2",30,5,27)
		TNT1 A 0 A_playsound("SSHFIRE", 3)
		PKSS B 2 BRIGHT A_SetPitch(-6.0 + pitch)
		PKSS C 1
		PKSS DEFAAA 1 A_SetPitch(1.0 + pitch)
		PKSS A 5
		TNT1 A 0 A_CheckReload
		PKSS GHIJKL 1
		TNT1 A 0 A_OpenShotgun2
		PKSS MNOPQR 1
		TNT1 AA 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10)
		PKES S 4
		PKSS STUV 2
		TNT1 A 0 A_LoadShotgun2
		PKSS XWYZ 1
		//TNT1 A 10
		PKS2 ABCD 2
		TNT1 A 0 A_PlaySound("weapons/sshotc")
		PKS2 EFG 2
		PKSS A 5
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		SGN2 A -1
		Stop
	}
}




ACTOR ClassicChaingun : DoomWeapon Replaces ChainGun
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "$GOTCHAINGUN"
	Obituary "$OB_MPCHAINGUN"
	+WEAPON.NOAUTOAIM
	Tag "$TAG_CHAINGUN"
	States
	{
	Ready:
	    PKCG A 1 A_WeaponReady
		Loop
	Deselect:
		PKCG A 1 A_Lower
		Loop
	Select:
	    PKCG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 BRIGHT A_FireBullets(5.6, 0, 1, 5, "MachineGunBulletPuff")
        TNT1 A 0 A_FireCustomMissile("Tracer", random(-3,3), 0, -1, -12, 0, random(-3,3))
		TNT1 A 0 A_PlaySound("CHGNSHOT", 1)
		TNT1 A 0 A_FireCustomMissile("RifleCaseSpawn",5,0,6,-6)
		PKCG F 1 BRIGHT 
		TNT1 A 0 A_SetPitch(-1.0 + pitch)
		PKCG B 1 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		PKCG CD 1 A_SetPitch(0.5 + pitch)
		
		
		TNT1 A 0 BRIGHT A_FireBullets(5.6, 0, 1, 5, "MachineGunBulletPuff")
        TNT1 A 0 A_FireCustomMissile("Tracer", random(-3,3), 0, -1, -12, 0, random(-3,3))
		TNT1 A 0 A_PlaySound("CHGNSHOT", 1)
		TNT1 A 0 A_FireCustomMissile("RifleCaseSpawn",5,0,6,-6)
		PKCG E 1 BRIGHT 
		TNT1 A 0 A_SetPitch(-1.0 + pitch)
		PKCG B 1 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		PKCG CD 1 A_SetPitch(0.5 + pitch)
		
		TNT1 A 0 A_ReFire
		PKCG ABCDAABBCCCDDDD 1 A_WeaponReady(1)
		Goto Ready
	Spawn:
		MGUN A -1
		Stop
	}
}


ACTOR ClassicRocketLauncher : DoomWeapon Replaces RocketLauncher
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	+WEAPON.NOAUTOAIM
	Inventory.PickupMessage "$GOTLAUNCHER"
	Tag "$TAG_ROCKETLAUNCHER"
	States
	{
	Ready:
	    PKRL A 1 A_WeaponReady
		Loop
	Deselect:
		PKRL A 1 A_Lower
		Loop
	Select:
		PKRL A 1 A_Raise
		Loop
	Fire:
		PKRL BC 2 BRIGHT
		TNT1 A 0 A_PlaySound ("DSRFIRE")
		TNT1 A 0 A_FireCustomMissile("WeakRocket", 0, 1, 0, -10)
		TNT1 A 0 A_SetPitch(-2.0 + pitch)
		PKRL D 2 BRIGHT
		PKRL EFGHIJ 2 A_SetPitch(0.25 + pitch)
		PKRL AA 1 A_SetPitch(0.25 + pitch)
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		LAUN A -1
		Stop
	}
}


ACTOR ClassicPlasmaRifle : DoomWeapon Replaces PlasmaRifle
{
	Game Doom
	Weapon.SelectionOrder 1
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	+WEAPON.NOAUTOAIM
	Inventory.PickupMessage "$GOTPLASMA"
	Tag "$TAG_PLASMARIFLE"
	States
	{
	Ready:
	    3LSG A 1 A_WeaponReady
		Loop
	Deselect:
	    3LSG A 1 A_Lower
		Loop
	Select:
	    3LSG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_FireCustomMissile("ClassicPlasmaBall", 0, 1, 0, -10)
		3LSF EF 1 BRIGHT
		3KPL A 1
		TNT1 A 0 A_Refire
		3KPL ABCDEFF 1
		3KPL G 6
		3KPL FFEDCBA 1
		Goto Ready
	Spawn:
		PLAS A -1
		Stop
	}
}

ACTOR ClassicPlasmaBall
{
	Game Doom
	Radius 4
	Height 4
	Speed 25
	Damage 5
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	SeeSound "weapons/plasmaf"
	DeathSound "weapons/plasmax"
	damagetype Plasma
	Obituary "$OB_MPPLASMARIFLE"
	Species "Marines"
	+THRUSPECIES
	+MTHRUSPECIES
	States
	{
	DM:
		TNT1 A 0
		TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
		TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
		Goto Spawn1
	
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_ExecuteAlways(719, 0, 0, 0, 0)//Check if Coop

	Spawn1:
	    TNT1 A 1
		Goto Spawn2
	Spawn2:
		PLSS ABAB 1 Bright A_SpawnItem("BlueFlareSmall")
		Loop
	Death:
	    TNT1 A 0
		TNT1 A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360))
		PLSE ABC 2 BRIGHT A_SpawnItem("BlueFlare")
		PLSE DE 2 Bright A_SpawnItem("BlueFlareSmall")
		Stop
	}
}


ACTOR VanillaBFG9000 : DoomWeapon Replaces BFG9000
{
	Game Doom
	Height 20
	Weapon.SelectionOrder 2
	Weapon.AmmoUse 40
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTBFG9000"
	Tag "$TAG_BFG9000"
	States
	{
	Ready:
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		BFGG A 1 A_Lower
		Loop
	Select:
		BFGG A 1 A_Raise
		Loop
	Fire:
        TNT1 A 0 A_PlaySound("BFGCHARGE")
		BFGC ABABABABABABABAB 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        BFGF A 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        TNT1 A 0 A_PlaySound("BFGFIRE")
		TNT1 AAA 0 A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        TNT1 A 0 A_Recoil(8)
        //TNT1 A 0 A_ZoomFactor(1.3)
        TNT1 A 0 BRIGHT A_FireCustomMissile("Alerter", 0, 0, -1, 0)
		TNT1 A 0 A_FireBFG
		BFGF B 2 BRIGHT// A_FireCustomMissile("SuperBFGBall", 0, 1, 0, 0)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
        BFGF CDEF 2 BRIGHT A_FireCustomMissile("GreenFlareSpawn",0,0,0,0)
        //TNT1 A 0 A_ZoomFactor(1.0)
        BFGG A 1 BRIGHT
		BFGG A 4
        TNT1 A 0  A_Refire
		Goto Ready
	}
}

Actor SuperBFGBall : BFGBall Replaces BFGBall
{
    +EXTREMEDEATH
	Damage 100
  Decal BFGLightning
	DeathSound "BFGEXPLO"
    DamageType Desintegrate
	Scale 0.7
	Species "Marines"
	+THRUSPECIES
	+MTHRUSPECIES
  States
  {
  DM:
		TNT1 A 0
		TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
		TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
		Goto Spawn1
	
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_ExecuteAlways(719, 0, 0, 0, 0)//Check if Coop

	Spawn1:
    TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0)
    BFS1 A 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
    BFS1 A 1 BRIGHT A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
    TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0)
    BFS1 B 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
    BFS1 B 1 BRIGHT A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
    Loop
 
 Death:
    TNT1 A 0
    //TNT1 A 0 Radius_Quake(4,34,0,12,0)
	
	TNT1 A 0 Bright A_SpawnItem("GreenShockWave",0,0,0)
    //BFE1 A 0 A_SpawnItemEx("BFGDeathParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
    //BFE1 A 0 A_SpawnItemEx("BFGSuperParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
	EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360))
	EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360))
	EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGDeathParticleSuperFast", 0, 0, random (0, 360), 2, random (0, 360))
	EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360))
	EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGDeathParticleFast", 0, 0, random (0, 360), 2, random (0, 360))
    TNT1 A 0 A_SpawnItem("MegaGibRemoving")
	//TNT1 A 0 A_Explode(80,300,0)
	BFGB ABCDE 1 Bright A_SpawnItem("GreenFlare",0,0)
    BFGB F 1 Bright A_BFGSpray//("SuperBFGExtra")
	TNT1 A 0 A_SPawnItem("SuperBFGExtraGiant")
    BFGB GHIJK 1 Bright A_SpawnItem("GreenFlare",0,0)
    Stop
  }
}

Actor BFGTrailParticle
{
  Height 0
  Radius 0
  Mass 0
  Speed 3
  +Missile
  +NoBlockMap
  +NoGravity
  +DontSplash
  RenderStyle Add
  Scale 0.05
  States
  {
  Spawn:
    SPKG A 2 Bright A_FadeOut(0.02)
    Loop
  }
}

Actor SuperBFGExtra : BFGExtra Replaces BFGExtra
{
  +NoBlockMap
  +NoGravity
  RenderStyle Add
  +FORCERADIUSDMG
  +THRUGHOST
  Alpha 0.75
  Damage 0
  Scale 0.6
  Radius 1
  Height 1
  DamageType "Desintegrate"
  States
  {
  Spawn:
    BFE2 A 0
	TNT1 A 0 A_Explode(40, 3)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
	BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium",0,0)
    stop
  }
}

Actor SuperBFGExtraGiant : SuperBFGExtra
{
  Scale 2.9
  States
  {
  Spawn:
    BFE2 A 0
    BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium",0,0)
	Stop
  }
}

Actor BFGDeathParticle
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  -NoGravity
  +LowGravity
  +DontSplash
  +DoomBounce
  BounceFactor 0.5
  RenderStyle Add
  Scale 0.04
  States
  {
  Spawn:
  Death:
    SPKG A 2 Bright A_FadeOut(0.02)
    Loop
  }
}

Actor BFGDeathParticleFast: BFGDeathParticle
{
  Speed 10
  Scale 0.1
  States
  {
  Spawn:
  Death:
	SPKG A 11 BRIGHT
    SPKG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 Bright A_FadeOut(0.06)
    Stop
  }
}

Actor BFGDeathParticleSuperFast: BFGDeathParticleFast
{
  Speed 20
}

Actor BFGExtraParticle
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  +DontSplash
  +NoInteraction
  RenderStyle Add
  Scale 0.035
  States
  {
  Spawn:
  Death:
    SPKG A 2 Bright A_FadeOut(0.1)
    Loop
  }
}

ACTOR BFGFOG
{
    Radius 1
    Height 1
	Alpha 0.7
	RenderStyle Add
	Scale 0.4
	Speed 8
	Gravity 0
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        BFOG ABCDEF 6 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
        Goto Death
	Death:
	    BFOG G 3 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
		Stop
    }
}

ACTOR BFGBIGFOG: BFGFOG
{
	Scale 0.8
}

ACTOR SmallGreenFog: BFGFOG
{
	Speed 3
	Scale 0.5
}